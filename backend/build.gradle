// Root build.gradle for multi-module project
plugins {
    id "java"
    id 'org.springframework.boot' version '3.5.9' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'com.google.protobuf' version '0.9.4' apply false
    id 'org.flywaydb.flyway' version '11.13.2' apply false
    id "org.springdoc.openapi-gradle-plugin" version "1.9.0" apply false
}

// Configuration common to all sub-projects/modules
allprojects {
    group = "ec.com.ecommerce"

    repositories {
        mavenCentral()
    }

    ext {
        set("springCloudVersion", "2025.0.0")
        set('springGrpcVersion', "0.11.0")
        set('grpcVersion', '1.78.0')
        set('protobufVersion', '4.33.2')
        set('testcontainersVersion', '1.20.2')
        set('flywayVersion', '11.20.0')
        set('grpcStarterVersion', '3.1.0.RELEASE')
    }
}

// Configuration specific to each sub-project/module
subprojects {
    apply plugin: "java"
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    java {
        sourceCompatibility = JavaVersion.VERSION_25
        targetCompatibility = JavaVersion.VERSION_25
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << '-parameters'
    }

    configurations {
        flyway // used by Flyway plugin to resolve database drivers
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        // Common dependencies for all modules
        implementation 'org.springframework.boot:spring-boot-starter'
        implementation 'org.springframework.boot:spring-boot-starter-logging'
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j'

        // Testing dependencies
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testImplementation 'org.springframework.boot:spring-boot-testcontainers'
        testImplementation "org.testcontainers:junit-jupiter:${property('testcontainersVersion')}"
        testImplementation "org.testcontainers:postgresql:${property('testcontainersVersion')}"


        developmentOnly 'org.springframework.boot:spring-boot-devtools'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${property('springCloudVersion')}"
        }
    }

    // Add Flyway Postgres dependency only where the Flyway plugin is applied
    plugins.withId("org.flywaydb.flyway") {
        dependencies {
            flyway "org.flywaydb:flyway-database-postgresql:${property('flywayVersion')}"
        }
    }

    test {
        useJUnitPlatform()
    }

    // Configure bootRun task for local development with hot reload
    tasks.withType(org.springframework.boot.gradle.tasks.run.BootRun) {
        // Enable Spring DevTools
        systemProperty 'spring.devtools.restart.enabled', 'true'
        systemProperty 'spring.devtools.livereload.enabled', 'true'

        // JVM args for development
        jvmArgs = ['-Xmx2g',
                   '-XX:MaxMetaspaceSize=512m',
                   '-XX:+HeapDumpOnOutOfMemoryError',
                   '-XX:HeapDumpPath=/tmp/heapdump.hprof',
                   '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=*:5005']

        // Source resources from build output for hot reload
        sourceResources sourceSets.main
    }
}

// =================================
// BUILD ORDER CONFIGURATION
// =================================
// Ensure grpc-lib builds first, then other libs, then core, then services

// 1. Configure grpc-lib to always generate protos before building
project(":libs:grpc-lib")?.afterEvaluate {
    tasks.named("compileJava") {
        dependsOn project(":libs:grpc-lib").tasks.named("generateProto")
    }
    tasks.named("build") {
        dependsOn project(":libs:grpc-lib").tasks.named("generateProto")
    }
}

// 2. Make all other libs wait for grpc-lib
configure(subprojects.findAll { it.path.startsWith(":libs:") && it.name != "grpc-lib" }) {
    afterEvaluate {
        tasks.named("build") {
            mustRunAfter project(":libs:grpc-lib").tasks.named("build")
        }
    }
}

// 3. Make core modules wait for all libs
configure(subprojects.findAll { it.path.startsWith(":core:") }) {
    afterEvaluate {
        def libProjects = subprojects.findAll { it.path.startsWith(":libs:") }
        tasks.named("build") {
            libProjects.each { libProject -> mustRunAfter libProject.tasks.named("build")
            }
        }
    }
}

// 4. Make services wait for core and libs
configure(subprojects.findAll { it.path.startsWith(":services:") }) {
    afterEvaluate {
        def libProjects = subprojects.findAll { it.path.startsWith(":libs:") }
        def coreProjects = subprojects.findAll { it.path.startsWith(":core:") }

        tasks.named("build") {
            libProjects.each { libProject -> mustRunAfter libProject.tasks.named("build")
            }
            coreProjects.each { coreProject -> mustRunAfter coreProject.tasks.named("build")
            }
        }
    }
}

// =================================
// CORE MODULES CONFIGURATION
// =================================
// Disable bootJar for the core parent module if it exists
project.findProject(":core")?.with {
    tasks.named("bootJar") {
        enabled = false
    }
    tasks.named("jar") {
        enabled = true
    }
}

configure(subprojects.findAll { it.path.startsWith(":core:") }) {
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    dependencies {
        implementation 'org.springframework:spring-context'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
    }

    if (project.name == "auth-service") {
        apply plugin: "com.google.protobuf"

        dependencies {
            implementation 'org.springframework.grpc:spring-grpc-spring-boot-starter'
            implementation 'org.springframework.kafka:spring-kafka'
            testImplementation 'org.springframework.grpc:spring-grpc-test'

            implementation project(":libs:commons")
            implementation project(":libs:grpc-lib")
            implementation project(":libs:paging-and-sorting")
            implementation project(":libs:component-scan")
        }

        dependencyManagement {
            imports {
                mavenBom "org.springframework.grpc:spring-grpc-dependencies:${property('springGrpcVersion')}"
            }
        }

        protobuf {
            protoc {
                artifact = "com.google.protobuf:protoc:${protobufVersion}"
            }
            plugins {
                grpc {
                    artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
                }
            }
            generateProtoTasks {
                all()*.plugins {
                    grpc {
                        option '@generated=omit'
                    }
                }
            }
        }

        // Ensure proto generation happens before compilation
        afterEvaluate {
            tasks.named("compileJava") {
                dependsOn project.tasks.named("generateProto")
            }
        }
    }
}

// =================================
// SERVICES MODULES CONFIGURATION
// =================================
// Disable bootJar for the services parent module if it exists
project.findProject(":services:")?.with {
    tasks.named("bootJar") {
        enabled = false
    }
    tasks.named("jar") {
        enabled = true
    }
}

configure(subprojects.findAll { it.path.startsWith(":services:") }) {
    apply plugin: "org.springframework.boot"
    apply plugin: "com.google.protobuf"
    apply plugin: "org.flywaydb.flyway"
    apply plugin: "org.springdoc.openapi-gradle-plugin"

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-batch'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'org.springframework.grpc:spring-grpc-spring-boot-starter'
        implementation 'org.flywaydb:flyway-core'
        implementation 'org.flywaydb:flyway-database-postgresql'
        implementation 'org.springframework.cloud:spring-cloud-starter-config'
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
        implementation 'org.springframework.kafka:spring-kafka'
        runtimeOnly 'org.postgresql:postgresql'
        testRuntimeOnly "com.h2database:h2"
        testImplementation 'org.springframework.batch:spring-batch-test'
        testImplementation 'org.springframework.grpc:spring-grpc-test'
        testImplementation 'org.springframework.kafka:spring-kafka-test'
        testImplementation "org.testcontainers:junit-jupiter"
        testImplementation "org.testcontainers:postgresql"
        implementation 'org.mapstruct:mapstruct:1.6.3'
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13'
        implementation 'org.springframework.boot:spring-boot-starter-data-redis'
        implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
        implementation "com.google.protobuf:protobuf-java:${protobufVersion}"

        implementation project(":libs:commons")
        implementation project(":libs:grpc-lib")
        implementation project(":libs:paging-and-sorting")
        implementation project(":libs:component-scan")
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.grpc:spring-grpc-dependencies:${property('springGrpcVersion')}"
        }
    }

    flyway {
        url = "jdbc:postgresql://${System.getenv('DB_HOST') ?: 'localhost'}:${System.getenv('DB_PORT') ?: '5432'}/${System.getenv('DB_NAME') ?: 'mydb'}"
        user = System.getenv('DB_USER') ?: 'user'
        password = System.getenv('DB_PASSWORD') ?: 'password'
        locations = ["classpath:${System.getenv('DB_MIGRATION_PATH') ?: 'db/migration'}"]
        schemas = [System.getenv('DB_SCHEMA') ?: 'public']
        baselineOnMigrate = true
        validateOnMigrate = true
        placeholderReplacement = true
        placeholderPrefix = '@@'
        placeholderSuffix = '@@'
        placeholders = ['GLOBAL_DB_HOST'    : System.getenv('GLOBAL_DB_HOST') ?: 'localhost',
                        'GLOBAL_DB_PORT'    : System.getenv('GLOBAL_DB_PORT') ?: '5432',
                        'GLOBAL_DB_NAME'    : System.getenv('GLOBAL_DB_NAME') ?: 'global_service_db',
                        'GLOBAL_DB_NAME'    : System.getenv('GLOBAL_DB_NAME') ?: 'global_service_db',
                        'GLOBAL_DB_USER'    : System.getenv('GLOBAL_DB_USER') ?: 'postgres',
                        'GLOBAL_DB_USERNAME': System.getenv('GLOBAL_DB_USERNAME') ?: 'postgres',
                        'GLOBAL_DB_PASSWORD': System.getenv('GLOBAL_DB_PASSWORD') ?: 'password']
    }

    protobuf {
        protoc {
            artifact = "com.google.protobuf:protoc:${protobufVersion}"
        }
        plugins {
            grpc {
                artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
            }
        }
        generateProtoTasks {
            all()*.plugins {
                grpc {
                    option '@generated=omit'
                }
            }
        }
    }

    // Ensure proto generation happens before compilation
    afterEvaluate {
        tasks.named("compileJava") {
            dependsOn project.tasks.named("generateProto")
            // Ensure grpc-lib is compiled first so MapStruct can see its generated protobuf types
            dependsOn project(":libs:grpc-lib").tasks.named("classes")
        }
    }

    openApi {
        outputDir.set(layout.buildDirectory.dir("generated/openapi"))
        outputFileName.set("${project.name}.yaml")
    }
}

// =================================
// LIBRARIES CONFIGURATION - Non-executable JARs
// =================================
// Disable bootJar for the libs parent module if it exists
project.findProject(":libs")?.with {
    tasks.named("bootJar") {
        enabled = false
    }
    tasks.named("jar") {
        enabled = true
    }
}

configure(subprojects.findAll { it.path.startsWith(":libs:") }) {
    apply plugin: "java"
    apply plugin: "java-library"

    // Disable Spring Boot executable jar/war for library modules
    tasks.named("bootJar") {
        enabled = false
    }

    tasks.named("jar") {
        enabled = true
    }

    if (project.name == "grpc-lib") {
        apply plugin: "com.google.protobuf"

        dependencies {
            api "io.grpc:grpc-protobuf:${grpcVersion}"
            api "io.grpc:grpc-stub:${grpcVersion}"
            api "com.google.protobuf:protobuf-java:${protobufVersion}"
        }

        protobuf {
            protoc {
                artifact = "com.google.protobuf:protoc:${protobufVersion}"
            }
            plugins {
                grpc {
                    artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
                }
            }
            generateProtoTasks {
                all()*.plugins {
                    grpc {
                        option '@generated=omit'
                    }
                }
            }
        }

        sourceSets {
            main {
                java {
                    srcDirs 'build/generated/source/proto/main/grpc'
                    srcDirs 'build/generated/source/proto/main/java'
                }
            }
        }
    }
}